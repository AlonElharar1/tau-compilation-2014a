S -> Program
Program -> CLASS Program | CLASS
CLASS -> class CLASS_ID { CLASS_CONTENT } | class CLASS_ID extends CLASS_ID { CLASS_CONTENT }
CLASS_CONTENT -> FIELD CLASS_CONTENT | METHOD CLASS_CONTENT | FIELD | METHOD
FIELD -> TYPE ID ; | TYPE IDS ;
IDS -> ID | IDS , ID
METHOD -> METHODSTATIC | METHODVIRTUAL
METHODSTATIC -> static METHOD_HEADER
METHODVIRTUAL -> METHOD_HEADER
METHOD_HEADER -> TYPE ID METHOD_PARAMS METHOD_CONTENT
METHOD_CONTENT -> { } | { STMTS }
STMTS -> STMT STMTS | STMT
METHOD_PARAMS -> ( ) | ( FORMALS )
TYPE -> TYPE [ ] | PRIMITIVE | CLASS_TYPE
CLASS_TYPE -> CLASS_ID
PRIMITIVE -> int | boolean | string | void
FORMALS -> PARAMETER | PARAMETER , FORMALS
PARAMETER -> TYPE ID
STMT -> STMTASSIGN | STMTCALL | STMTRETURN | STMTIF | STMTWHILE | STMTBREAK | STMTCONTINUE | STMTBLOCK | STMTLOCALVAR 
STMTASSIGN -> LOCATION = EXPR ;
STMTCALL -> CALL ;
STMTRETURN -> return ; | return EXPR ;
STMTIF -> if ( EXPR ) STMT | if ( EXPR ) STMT else STMT
STMTWHILE -> while ( EXPR ) STMT
STMTBREAK -> break ;
STMTCONTINUE -> continue ;
STMTBLOCK -> { STMTS }
STMTLOCALVAR -> TYPE ID ; | TYPE ID = EXPR ;
EXPR -> EXPRBINOP | CALL | EXPRBLOCK | EXPRLEN | LITERAL | EXPRNEW | LOCATION | EXPRTHIS | EXPRUNOP
EXPRBINOP -> EXPR BINOP EXPR
BINOP -> + | - | * | / | % | && | || | < | <= | > | >= | == | !=
CALL -> STATIC_CALL | VIRTUAL_CALL
STATIC_CALL -> CLASS_ID . ID ( ) | CLASS_ID . ID ( EXPRS )
VIRTUAL_CALL -> EXPR . ID ( ) | ID ( ) | EXPR . ID ( EXPRS ) | ID ( EXPRS )
EXPRS -> EXPR | EXPR , EXPRS
EXPRBLOCK -> ( EXPR )
EXPRLEN -> EXPR . length
LITERAL -> INTEGER | STRING | true | false | null
EXPRNEW -> EXPRNEWARRAY | EXPRNEWINS
EXPRNEWARRAY -> new TYPE [ EXPR ]
EXPRNEWINS -> new CLASS_ID ( )
LOCATION -> REFARRAY | REFFIELD | REFVAR
REFARRAY -> EXPR [ EXPR ]
REFFIELD -> EXPR . ID
REFVAR -> ID
EXPRTHIS -> this
EXPRUNOP -> UNOP EXPR
UNOP -> - | !