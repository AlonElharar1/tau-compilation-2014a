S -> Program
Program -> CLASS Program | CLASS
CLASS -> class CLASS_ID { CLASS_CONTENT } | class CLASS_ID extends CLASS_ID { CLASS_CONTENT }
CLASS_CONTENT -> FIELD CLASS_CONTENT | METHOD CLASS_CONTENT | FIELD | METHOD
FIELD -> TYPE ID ; | TYPE IDS ;
IDS -> ID | IDS , ID
METHOD -> METHODSTATIC | METHODVIRTUAL
METHODSTATIC -> static METHOD_HEADER
METHODVIRTUAL -> METHOD_HEADER
METHOD_HEADER -> TYPE ID METHOD_PARAMS METHOD_CONTENT
METHOD_CONTENT -> { } | { STMTS }
STMTS -> STMT STMTS | STMT
METHOD_PARAMS -> ( ) | ( FORMALS )
TYPE -> TYPE [ ] | PRIMITIVE | CLASS_TYPE
CLASS_TYPE -> CLASS_ID
PRIMITIVE -> int | boolean | string | void
FORMALS -> PARAMETER | PARAMETER , FORMALS
PARAMETER -> TYPE ID
STMT -> STMTASSIGN | STMTCALL | STMTRETURN | STMTIF | STMTWHILE | STMTBREAK | STMTCONTINUE | STMTBLOCK | STMTLOCALVAR
NON_IF_STMT -> STMTASSIGN | STMTCALL | STMTRETURN | STMTWHILE | STMTBREAK | STMTCONTINUE | STMTBLOCK | STMTLOCALVAR
STMTASSIGN -> LOCATION = EXPR ;
STMTCALL -> CALL ;
STMTRETURN -> return ; | return EXPR ;
STMTIF -> MATCHED_IF | UNMATCHED_IF
MATCHED_IF -> if ( EXPR ) MATCHED_IF else MATCHED_IF | if ( EXPR ) NON_IF_STMT else MATCHED_IF | if ( EXPR ) MATCHED_IF else NON_IF_STMT | if ( EXPR ) NON_IF_STMT else NON_IF_STMT
UNMATCHED_IF -> if ( EXPR ) STMT | if ( EXPR ) MATCHED_IF else STMT | if ( EXPR ) NON_IF_STMT else STMT
STMTWHILE -> while ( EXPR ) STMT
STMTBREAK -> break ;
STMTCONTINUE -> continue ;
STMTBLOCK -> { STMTS }
STMTLOCALVAR -> TYPE ID ; | TYPE ID = EXPR ;
EXPR -> EXPRCLOSE | EXPRBINOP | EXPRUNOP | EXPRNEW
EXPRCLOSE -> CALL | EXPRBLOCK | LITERAL | EXPRTHIS | LOCATION | EXPRLEN
NO_BINOP_EXPR -> EXPRCLOSE | EXPRUNOP | EXPRNEW
UNOP -> - | !
EQOP -> == | !=
RELOP -> < | <= | > | >=
ADDOP -> + | -
MULTOP -> * | / | %
EXPRBINOP -> NO_BINOP_EXPR || EXPRBINOP_AND | EXPRBINOP_AND 
EXPRBINOP_AND -> EXPRBINOP_AND && EXPRBINOP_EQ | EXPRBINOP_EQ
EXPRBINOP_EQ -> EXPRBINOP_EQ EQOP EXPRBINOP_RELATIONAL | EXPRBINOP_RELATIONAL
EXPRBINOP_RELATIONAL -> EXPRBINOP_RELATIONAL RELOP EXPRBINOP_ADD  | EXPRBINOP_ADD
EXPRBINOP_ADD -> EXPRBINOP_ADD ADDOP EXPRBINOP_MULT | EXPRBINOP_MULT
EXPRBINOP_MULT -> EXPRBINOP_MULT MULTOP LITERAL | NO_BINOP_EXPR
CALL -> STATIC_CALL | VIRTUAL_CALL
STATIC_CALL -> CLASS_ID . ID ( ) | CLASS_ID . ID ( EXPRS )
VIRTUAL_CALL -> EXPRCLOSE . ID ( ) | ID ( ) | EXPRCLOSE . ID ( EXPRS ) | ID ( EXPRS )
EXPRS -> EXPR | EXPR , EXPRS
EXPRBLOCK -> ( EXPR )
EXPRLEN -> EXPRCLOSE . length
LITERAL -> INTEGER | STRING | true | false | null
EXPRNEW -> EXPRNEWARRAY | EXPRNEWINS
EXPRNEWARRAY -> new TYPE [ EXPR ]
EXPRNEWINS -> new CLASS_ID ( )
LOCATION -> REFARRAY | REFFIELD | REFVAR
REFARRAY -> EXPRCLOSE [ EXPR ]
REFFIELD -> EXPRCLOSE . ID
REFVAR -> ID
EXPRTHIS -> this
EXPRUNOP -> UNOP EXPR